apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'maven'

ext {
    pluginGroupId = 'com.lenebf.plugin'
    pluginVersion = releaseVersion
}
def pomConfig = {
    licenses {
        license {
            name "GNU General Public License v3.0"
            url "https://www.gnu.org/licenses/gpl-3.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "lenebf"
            name "lenebf"
            email "lenebf@126.com"
        }
    }
    scm {
        url "https://github.com/lenebf/AppCatcher"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    def classifier = getArchiveClassifier()
    classifier.set('source')
    classifier.convention('source')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    def classifier = getArchiveClassifier()
    classifier.set('javadoc')
    classifier.convention('javadoc')
    from javadoc.destinationDir
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId pluginGroupId
            artifactId 'ac'
            version pluginVersion
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'App catcher gradle plugin.')
                root.appendNode('name', 'ac-plugin')
                root.appendNode('url', 'https://github.com/lenebf/AppCatcher')
                root.children().last() + pomConfig
            }
        }
    }
}

// Bintray keys
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.key")
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'ac-plugin'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/lenebf/AppCatcher'

        version {
            name = pluginVersion
            desc = "App Catcher Plugin $pluginVersion"
            released = new Date()
            // vcsTag = 'tag_release_1.0.1'
            // attributes = ['ac-plugin': 'com.lenebf.plugin:ac']
        }
    }
}

gradlePlugin {
    // Define the plugin
    plugins {
        ac {
            id = 'com.lenebf.plugin.ac'
            implementationClass = 'com.lenebf.plugin.AppCatcherPlugin'
        }
    }
}

dependencies {
    implementation 'com.android.tools.build:gradle:4.0.1'
    implementation 'org.ow2.asm:asm:8.0.1'
    implementation 'org.ow2.asm:asm-commons:8.0.1'
}
